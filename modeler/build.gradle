plugins {
    id "org.springframework.boot" version "2.4.2"
}
apply plugin: "java"
apply plugin: "io.spring.dependency-management"

group = "net.sayaya"
version = "1.0"
java.sourceCompatibility = JavaVersion.VERSION_11
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation project(":data")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation("org.springframework.boot:spring-boot-starter-data-cassandra")
    implementation("org.springframework.cloud:spring-cloud-starter-zookeeper-discovery")

    implementation("io.springfox:springfox-boot-starter:3.0.0")
    implementation("io.springfox:springfox-swagger-ui")
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("io.projectreactor:reactor-test")
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.1"
    }
}
configurations {
    all*.exclude group:'org.springframework.boot', module:'spring-boot-starter-logging'
}
task copyWebResources(type: Copy) {
    dependsOn ":modeler-ui:build"
    from(zipTree ('../modeler-ui/build/libs/sayaya-document-classification-modeler-ui.war')) {
        include '**/*.js'
        include '**/*.css'
        include '**/*.png'
        include '**/*.gif'
        include '**/*.wav'
        include '**/*.ogg'
        include '**/*.svg'
        include '**/*.ttf'
        include '**/*.woff'
        include '**/*.woff2'
        include '**/*.eot'
        include '*.html'
        includeEmptyDirs = false
    }
    into "src/main/resources/static"
}
processResources {
    dependsOn copyWebResources
}
bootJar {
    archiveName "sayaya-document-classification-modeler.jar"
}
clean.doFirst {
    delete "src/main/resources/static"
    delete "build/"
}